# Maintainer: matmoul <matmoul at the google email domain which is .com>
# Contributor: enihcam <nanericwang at the google email domain which is .com>
# Contributor: Arun <cute.tec at the google email domain which is .com>

pkgname=walinuxagent
_pkgname=WALinuxAgent
pkgver=2.15.0.1
pkgrel=2
pkgdesc="Microsoft Azure Linux Agent"
arch=('any')
url="https://github.com/Azure/WALinuxAgent"
license=('Apache-2.0')
depends=('python' 'python-distro')
makedepends=('python-setuptools')
backup=("etc/waagent.conf")
source=("https://github.com/Azure/WALinuxAgent/archive/v${pkgver}.tar.gz"
        "use-arch-udev-files-dest.patch")
sha256sums=('f405cb90179dbe582a1c71a36af8016072c0defeb969aeee7861d5aab10499d8'
            '0a460c3bdac39cc2c68f4c5707254c3108c6f792982757a746bfd19adbc48ca4')

prepare() {
	cd "$_pkgname-$pkgver"
    patch -Np1 -i $srcdir/use-arch-udev-files-dest.patch
}

package() {
    depends+=('net-tools' 'openssh' 'openssl' 'parted')
    cd "$_pkgname-$pkgver"
    python setup.py install --root="$pkgdir" --optimize=1
    install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
