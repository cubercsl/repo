# Maintainer: matmoul <matmoul at the google email domain which is .com>
# Contributor: enihcam <nanericwang at the google email domain which is .com>
# Contributor: Arun <cute.tec at the google email domain which is .com>

pkgname=walinuxagent
_pkgname=WALinuxAgent
pkgver=2.11.1.12
pkgrel=1
pkgdesc="Microsoft Azure Linux Agent"
arch=('any')
url="https://github.com/Azure/WALinuxAgent"
license=('Apache-2.0')
depends=('python' 'python-distro')
makedepends=('python-setuptools')
backup=("etc/waagent.conf")
source=("https://github.com/Azure/WALinuxAgent/archive/v${pkgver}.tar.gz"
        "use-arch-udev-files-dest.patch")
sha256sums=('faab56182f55b6dd44e7c018305637e4a1708d5388fe1a8524e020825eac5f92'
            '0a460c3bdac39cc2c68f4c5707254c3108c6f792982757a746bfd19adbc48ca4')

prepare() {
	cd "$_pkgname-$pkgver"
    patch -Np1 -i $srcdir/use-arch-udev-files-dest.patch
}

package() {
    depends+=('net-tools' 'openssh' 'openssl' 'parted')
    cd "$_pkgname-$pkgver"
    python setup.py install --root="$pkgdir" --optimize=1
    install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
