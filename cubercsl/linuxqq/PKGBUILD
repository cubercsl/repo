# Maintainer: Purofle <purofle@gmail.com>
# Contributor: Yidaozhan Ya <yidaozhan_ya@outlook.com>
# Contributor: Integral <luckys68@126.com>
pkgname=linuxqq
pkgver=3.2.5_21453
pkgrel=1
pkgdesc='New Linux QQ based on Electron'
arch=('x86_64')
url="https://im.qq.com/linuxqq/"
license=('custom')
depends=(
    'gtk3'              # libgtk-3-0
    'libnotify'         # libnotify4
    'nss'               # libnss3
    'libxss'            # libxss1
    'libxtst'           # libxtst6
    'xdg-utils'         # xdg-utils
    'at-spi2-core'      # libatspi
    'util-linux-libs'   # libuuid1
    'libsecret'         # libsecret
    'libvips'
)
optdepends=('libappindicator-gtk3: Allow QQ to extend a menu via Ayatana indicators in Unity, KDE or Systray (GTK+ 3 library).')
source=(
    "linuxqq.sh"
    "https://dldir1.qq.com/qqfile/qq/QQNT/Linux/QQ_${pkgver}_amd64_01.deb"
)
sha512sums=(
    '8c92a5dcc2651a4ffb73425abbd8a567c4f043ec5b0614505273511260560a25ce8db30c6848977378921d860dc0a73eca083299706a585461587a48949e175c'
    '453eb6093a69840e4282ce8400b7be7d78147618e3279f6dab135536facb097a169d328c142b319ffc33b8e29297d491d63628ca55f32fbe5453ef4305a62095'
)

package() {
    echo "  -> Extracting the data.tar.xz..."
    bsdtar -xvf data.tar.xz -C "${pkgdir}/"

    echo "  -> Installing..."
    # Launcher
    install -Dm755 "${srcdir}/linuxqq.sh" "${pkgdir}/usr/bin/${pkgname}"

    # Launcher Fix
    sed -i 's!Exec=.*!Exec=qq %U!' "${pkgdir}/usr/share/applications/qq.desktop"

    # Icon
    sed -i 's!Icon=.*!Icon=qq!' "${pkgdir}/usr/share/applications/qq.desktop"
    # seems like we don't need this small icon
    rm -rvf "${pkgdir}/usr/share/icons/hicolor/2x2"

    # License
    install -Dm644 "${pkgdir}/opt/QQ/LICENSE.electron.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm644 "${pkgdir}/opt/QQ/LICENSES.chromium.html" -t "${pkgdir}/usr/share/licenses/${pkgname}/"

    rm -f "${pkgdir}"/opt/QQ/resources/app/{libssh2.so.1,libunwind*,sharp-lib/libvips-cpp.so.42}
}
