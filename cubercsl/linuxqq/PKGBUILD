# Maintainer: Purofle <purofle@gmail.com>
# Contributor: Yidaozhan Ya <yidaozhan_ya@outlook.com>
# Contributor: Integral <luckys68@126.com>
pkgname=linuxqq
pkgver=3.2.23_260108
pkgrel=1
pkgdesc='New Linux QQ based on Electron'
arch=('x86_64')
url="https://im.qq.com/linuxqq/"
license=('custom')
depends=(
    'gtk3'              # libgtk-3-0
    'libnotify'         # libnotify4
    'nss'               # libnss3
    'libxss'            # libxss1
    'libxtst'           # libxtst6
    'xdg-utils'         # xdg-utils
    'at-spi2-core'      # libatspi
    'util-linux-libs'   # libuuid1
    'libsecret'         # libsecret
    'libvips'
)
optdepends=('libappindicator-gtk3: Allow QQ to extend a menu via Ayatana indicators in Unity, KDE or Systray (GTK+ 3 library).')
source=(
    "linuxqq.sh"
    "https://dldir1.qq.com/qqfile/qq/QQNT/Linux/QQ_${pkgver}_amd64_01.deb"
)
sha512sums=('e06676ac2297cba5d20877ac82ef506a9596980bc66257952f37d45ef9810953aedb789655d004b3fd0ac548f2f085e1be406081d9c8d5321622567431c7b3da'
            'acb42d676bdb9c64da4aa3f8ed1a2a4eaac73de75eac45928923f734fd34a2da00e90829b9a91d9d4c18fca3ee459c168b4217a2a462ce6aa32f95712cee87fe')

package() {
    echo "  -> Extracting the data.tar.xz..."
    bsdtar -xvf data.tar.xz -C "${pkgdir}/"

    echo "  -> Installing..."
    # Launcher
    install -Dm755 "${srcdir}/linuxqq.sh" "${pkgdir}/usr/bin/qq"

    # Launcher Fix
    sed -i 's!Exec=.*!Exec=qq %U!' "${pkgdir}/usr/share/applications/qq.desktop"

    # Icon
    sed -i 's!Icon=.*!Icon=qq!' "${pkgdir}/usr/share/applications/qq.desktop"
    # seems like we don't need this small icon
    rm -rvf "${pkgdir}/usr/share/icons/hicolor/2x2"

    # License
    install -Dm644 "${pkgdir}/opt/QQ/LICENSE.electron.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm644 "${pkgdir}/opt/QQ/LICENSES.chromium.html" -t "${pkgdir}/usr/share/licenses/${pkgname}/"

    # Temporary Solution: Remove libvips and libssh2 which comes from package "linuxqq" itself
    rm -fv ${pkgdir}/opt/QQ/resources/app/{sharp-lib/libvips-cpp.so.42,{,avsdk/bugly/}libssh2.so.1}
}
