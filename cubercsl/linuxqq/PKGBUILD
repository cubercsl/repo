# Maintainer: Purofle <purofle@gmail.com>
# Contributor: Yidaozhan Ya <yidaozhan_ya@outlook.com>
# Contributor: Integral <luckys68@126.com>
pkgname=linuxqq
_md5_prefix=4cb54015
pkgver=3.2.1_17816
pkgrel=1
pkgdesc='New Linux QQ based on Electron'
arch=('x86_64' 'aarch64')
url="https://im.qq.com/linuxqq/"
license=('custom')
depends=(
    'gtk3'              # libgtk-3-0
    'libnotify'         # libnotify4
    'nss'               # libnss3
    'libxss'            # libxss1
    'libxtst'           # libxtst6
    'xdg-utils'         # xdg-utils
    'at-spi2-core'      # libatspi
    'util-linux-libs'   # libuuid1
    'libsecret'         # libsecret
    'libvips'
)
optdepends=('libappindicator-gtk3: Allow QQ to extend a menu via Ayatana indicators in Unity, KDE or Systray (GTK+ 3 library).')
source_x86_64=("https://dldir1.qq.com/qqfile/qq/QQNT/${_md5_prefix}/linuxqq_${pkgver//_/-}_amd64.deb")
source_aarch64=("https://dldir1.qq.com/qqfile/qq/QQNT/${_md5_prefix}/linuxqq_${pkgver//_/-}_arm64.deb")
sha512sums_x86_64=('6ec77d5b5efc21b9ed9b6a66e944179c79f638d49e310b0d2e8dbc553406dd49bdb858fc785ceddeeb9a95308b23e3a127ef4a1dcef83f13bc423402efb60e51')
sha512sums_aarch64=('6d842dd78db1daca3b96d9f79e57f5e6edf81866afdfd192d2417807140bf045afcc9c2daee8b829b3f11737fc830660c229c7a0a49b412bb6370f789081c597')

package() {
    echo "  -> Extracting the data.tar.xz..."
    bsdtar -xvf data.tar.xz -C "${pkgdir}/"

    echo "  -> Installing..."
    # Launcher
    install -d "${pkgdir}/usr/bin/"
    ln -s "/opt/QQ/qq" "${pkgdir}/usr/bin/qq"

    # Launcher Fix
    sed -i 's!Exec=.*!Exec=qq %U!' "${pkgdir}/usr/share/applications/qq.desktop"

    # Icon
    sed -i 's!Icon=.*!Icon=qq!' "${pkgdir}/usr/share/applications/qq.desktop"
    # seems like we don't need this small icon
    rm -rvf "${pkgdir}/usr/share/icons/hicolor/2x2"

    # License
    install -Dm644 "${pkgdir}/opt/QQ/LICENSE.electron.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm644 "${pkgdir}/opt/QQ/LICENSES.chromium.html" -t "${pkgdir}/usr/share/licenses/${pkgname}/"

    rm -f "${pkgdir}"/opt/QQ/resources/app/{libssh2.so.1,libunwind*,sharp-lib/libvips-cpp.so.42}
}
