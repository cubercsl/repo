# Maintainer: cubercsl <2014cais01 at gmail dot com>
pkgname=tiny-dfr
pkgver=0.3.4
pkgrel=1
pkgdesc="Apple silicon touch bar input / display daemon"
url="https://github.com/AsahiLinux/tiny-dfr"
arch=('x86_64')
license=("MIT")
depends=(pango libinput gdk-pixbuf2 libxml2 librsvg)
makedepends=(git cargo)
source=("$pkgname-$pkgver.tar.gz::https://github.com/AsahiLinux/tiny-dfr/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('e842c557a8c547c97e9b5a3876da191aa750e198fe566f8f35ddee6445a38eb6')

build() {
    cd "$pkgname-$pkgver"

	cargo build --release --locked --all-features
}


package() {
    cd "$pkgname-$pkgver"

	install -Dm755 target/release/$pkgname "$pkgdir"/usr/bin/$pkgname
	install -Dm644 share/$pkgname/*.svg -t "$pkgdir/usr/share/$pkgname"
	install -Dm644 share/$pkgname/config.toml -t "$pkgdir/usr/share/$pkgname"
	install -Dm644 etc/udev/rules.d/99-touchbar-seat.rules "$pkgdir/usr/lib/udev/rules.d/99-touchbar-seat.rules"
	install -Dm644 etc/udev/rules.d/99-touchbar-tiny-dfr.rules "$pkgdir/usr/lib/udev/rules.d/99-touchbar-tiny-dfr.rules"
    install -Dm644 etc/systemd/system/*.service -t "$pkgdir/usr/lib/systemd/system"
}
