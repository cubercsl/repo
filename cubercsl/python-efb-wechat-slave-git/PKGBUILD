# Maintainer: Ke Liu <spcter119@gmail.com>

pkgname=python-efb-wechat-slave-git
_provide=${pkgname%-git}
_name=${_provide#python-}
pkgver=r293.4755d17
pkgrel=1
pkgdesc='EFB WeChat Slave, a channel for EH Forwarder Bot.'
arch=('any')
url='https://github.com/blueset/efb-wechat-slave'
license=('AGPL3')
groups=('efb')
depends=(
	'python-ehforwarderbot'
	'python-itchat'
	'python-magic-ahupp'
	'python-pillow'
	'libwebp'
	'python-pyqrcode'
	'python-yaml'
	'python-requests'
	'python-typing_extensions'
	'python-bullet'
	'python-cjkwrap')
makedepends=(
	'git'
	'python-setuptools')
provides=($_provide)
conflicts=($_provide)
source=("$_provide"::"git+${url}.git")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_provide"
	printf "%s" "$(git describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "$srcdir/$_provide"
	python setup.py build
	cd "build/lib/${_name//-/_}/locale/"
	rm -rf ach_UG
	for _locale in $(ls); do
		(cd "$_locale/LC_MESSAGES/"; msgfmt "${_name//-/_}.po" -o "${_name//-/_}.mo")
	done
}

package() {
	cd "$srcdir/$_provide"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
	install -Dm644 "$srcdir/$_provide/LICENSE.md" "$pkgdir/usr/share/licenses/$_provide/LICENSE.md"
}

