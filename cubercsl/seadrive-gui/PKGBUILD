# Maintainer: cubercsl

pkgname=seadrive-gui
pkgver=2.0.21
pkgrel=1
pkgdesc="GUI part of seadrive"
arch=('i686' 'x86_64' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/haiwen/${pkgname}"
license=('Apache')
depends=('qt6-webengine' 'qt6-tools' 'qt6-5compat' 
         'seadrive-daemon')
makedepends=("cmake")
source=("${pkgname}-v${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('4134f3b42288b5d6e3d9538ca33cb36db6a32272ab1e47ad062292b68b1d8a0c')

prepare() {
  mkdir "${srcdir}/bin"
  ln -sf $(pacman -Ql qt6-base | grep /usr/bin/qmake | awk '{print $2}') ${srcdir}/bin/qmake
}

build () {
  cd "${srcdir}"
  mkdir -p build && cd build
  export PATH="${srcdir}/bin":$PATH
  cmake \
    -DBUILD_SPARKLE_SUPPORT=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${srcdir}/${pkgname}-${pkgver}"

  make
}

package () {
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" install
}
