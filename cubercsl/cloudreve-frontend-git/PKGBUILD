# Maintainer: cubercsl <2014cais01 at gmail dot com>
pkgname=cloudreve-frontend-git
_pkgname=cloudreve
pkgver=r518.ceb0a80
pkgrel=1
pkgdesc='Cloudreve V3 的前端'
arch=('any')
url='https://github.com/cloudreve/frontend'
license=('GPL')
makedepends=('git' 'yarn')
provides=(cloudreve-frontend)
source=("$pkgname::git+https://github.com/cloudreve/frontend")
sha256sums=('SKIP')

pkgver() {
    cd "$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$pkgname"
    yarn install 
}

build() {
    cd "$pkgname"
    yarn run build
    cd build
    find . -name "*.map" -type f -delete
}

package() {
    cd "$pkgname"
    install -vdm755 "$pkgdir/usr/share/$_pkgname"
    cp -av build/*  "$pkgdir/usr/share/$_pkgname"
}
