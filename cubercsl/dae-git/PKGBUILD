# Maintainer: cubercsl <2014cais01 at gmail dot com>
pkgname=dae-git
_pkgname=${pkgname%-git}
pkgver=0.8.0rc1.r23.g218ae3f
pkgrel=1
pkgdesc="A Linux lightweight and high-performance transparent proxy solution based on eBPF."
arch=(x86_64)
url="https://github.com/daeuniverse/dae"
license=('AGPL')
makedepends=(clang llvm go git)
source=("$_pkgname::git+https://github.com/daeuniverse/dae")
sha256sums=('SKIP')
options=(!debug)

pkgver() {
    cd "$srcdir/$_pkgname"
    ( set -o pipefail
      git describe --tags --long --abbrev=7 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
      printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
    )
}

prepare() {
    cd "$srcdir/$_pkgname"
    git submodule update --init --recursive
}

build() {
    export GOFLAGS="-buildmode=pie -trimpath -modcacherw"
    export CFLAGS="-fno-stack-protector"
    cd "$srcdir/$_pkgname"
    make VERSION="unstable-$pkgver"
}

package() {
    provides=($_pkgname)
    conflicts=($_pkgname)
    depends=(
        glibc
        v2ray-geoip
        v2ray-domain-list-community
    )
    backup=("etc/dae/config.dae")

    cd "$srcdir/$_pkgname"
    install -vDm755 "$srcdir/$_pkgname/dae" "$pkgdir/usr/bin/dae"
    install -vDm644 "$srcdir/$_pkgname/install/dae.service" "$pkgdir/usr/lib/systemd/system/dae.service"
    install -vDm640 "$srcdir/$_pkgname/install/empty.dae" "$pkgdir/etc/dae/config.dae"
    install -vDm644 "$srcdir/$_pkgname/example.dae" "$pkgdir/etc/dae/config.dae.example"

    mkdir -p "$pkgdir/usr/share/dae"
    ln -vs /usr/share/v2ray/geoip.dat $pkgdir/usr/share/dae/geoip.dat 
    ln -vs /usr/share/v2ray/geosite.dat $pkgdir/usr/share/dae/geosite.dat 
}
