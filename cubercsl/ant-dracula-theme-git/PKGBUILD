# Maintainer: cubercsl <2014cais01 at gmail dot com>
# Contributor: Tony Lambiris <tony@libpcap.net>
# Contributor: Michael Serajnik <m at mser dot at>

pkgbase=ant-dracula-theme-git
pkgname=(ant-dracula-{,kde-,kvantum-}theme-git)
_pkgname=Dracula
pkgver=4.0.0.r112.fc59294c
pkgrel=1
pkgdesc="Dracula variant of the Ant theme"
arch=("any")
url="https://github.com/dracula/gtk"
license=('GPL')
makedepends=('git')
source=("${_pkgname}::git+https://github.com/dracula/gtk.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

package_ant-dracula-theme-git() {
  conflicts=('ant-dracula-gtk-theme')
  provides=('ant-dracula-gtk-theme')
  optdepends=('ttf-roboto: primary font face defined'
              'ttf-ubuntu-font-family: secondary font face defined'
              'cantarell-fonts: tertiary font face defined')

  cd "${_pkgname}"

  mkdir -p "${pkgdir}/usr/share/themes/${_pkgname}"
  cp -av "${srcdir}/${_pkgname}/"* "${pkgdir}/usr/share/themes/${_pkgname}/"
  rm -rf "${pkgdir}/usr/share/themes/${_pkgname}/kde"
}

package_ant-dracula-kde-theme-git() {
  conflicts=("ant-dracula-kde-theme")
  provides=("ant-dracula-kde-theme")
  pkgdesc="Ant Dracula theme for KDE Plasma"
  optdepends=("ant-dracula-gtk-theme: Matching GTK theme"
              "ant-dracula-kvantum-theme-git: Ant Dracula theme for Kvantum Qt style (recommended)")

  cd "${_pkgname}/kde"

  mkdir -p "${pkgdir}/usr/share/aurorae/themes" "${pkgdir}/usr/share/sddm/themes"

  cp -rv aurorae/* "${pkgdir}/usr/share/aurorae/themes"
  cp -rv color-schemes "${pkgdir}/usr/share"
  cp -rv plasma "${pkgdir}/usr/share"
  cp -rv sddm/* "${pkgdir}/usr/share/sddm/themes"
}

package_ant-dracula-kvantum-theme-git() {
  conflicts=("ant-dracula-kvantum-theme")
  provides=("ant-dracula-kvantum-theme")
  pkgdesc="Ant Dracula theme for Kvantum Qt style"
  depends=(kvantum)

  cd "${_pkgname}/kde"

  mkdir -p "${pkgdir}/usr/share/Kvantum"

  cp -rv kvantum/* "${pkgdir}/usr/share/Kvantum"
}
